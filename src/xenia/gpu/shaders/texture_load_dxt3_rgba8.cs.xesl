/**
 ******************************************************************************
 * Xenia : Xbox 360 Emulator Research Project                                 *
 ******************************************************************************
 * Copyright 2022 Ben Vanik. All rights reserved.                             *
 * Released under the BSD license - see LICENSE in the root for more details. *
 ******************************************************************************
 */

#include "pixel_formats.xesli"
#include "texture_load.xesli"

xesl_writeTypedStorageBuffer_declare(xesl_uint4, xe_texture_load_dest, set=0,
                                     binding=0, u0, space0)
xesl_typedStorageBuffer_declare(xesl_uint4, xe_texture_load_source, set=1,
                                binding=0, t0, space0)
xesl_entry_bindings_begin_compute
  XE_TEXTURE_LOAD_CONSTANT_BUFFER_BINDING
  xesl_entry_binding_next
  xesl_writeTypedStorageBuffer_binding(xesl_uint4, xe_texture_load_dest,
                                       buffer(1))
  xesl_entry_binding_next
  xesl_typedStorageBuffer_binding(xesl_uint4, xe_texture_load_source, buffer(2))
xesl_entry_bindings_end_inputs_begin_compute
  xesl_entry_input_globalInvocationID
xesl_entry_inputs_end_code_begin_compute
  // 1 thread = 2 DXT3 blocks to 8x4 R8G8B8A8 texels.
  XeTextureLoadInfo load_info = XeTextureLoadGetInfo(
      xesl_function_call_constantBuffer(xe_texture_load_constants));
  xesl_uint3 block_index = xesl_GlobalInvocationID << xesl_uint3(1u, 0u, 0u);
  xesl_dont_flatten
  if (any(xesl_greaterThanEqual(block_index.xy, load_info.size_blocks.xy))) {
    return;
  }
  xesl_uint3 texel_index_host = block_index << xesl_uint3(2u, 2u, 0u);
  uint block_offset_host = uint(
      (XeTextureHostLinearOffset(xesl_int3(texel_index_host),
                                 load_info.host_pitch, load_info.height_texels,
                                 4u) +
       load_info.host_offset) >> 4u);
  uint elements_pitch_host = load_info.host_pitch >> 4u;
  uint block_offset_guest =
      XeTextureLoadGuestBlockOffset(load_info, block_index, 16u, 4u) >> 4u;
  uint i;
  xesl_unroll for (i = 0u; i < 2u; ++i) {
    if (i != 0u) {
      ++block_offset_host;
      // Odd block = even block + 32 guest bytes when tiled.
      block_offset_guest += load_info.is_tiled ? 2u : 1u;
    }
    xesl_uint4 block = XeEndianSwap32(
        xesl_typedStorageBufferLoad(xe_texture_load_source, block_offset_guest),
        load_info.endian_32);
    xesl_uint2 bgr_end_8in10 = XeDXTColorEndpointsToBGR8In10(block.z);
    // Sort the color indices so they can be used as weights for the second
    // endpoint.
    uint bgr_weights = XeDXTHighColorWeights(block.w);
    xesl_writeTypedStorageBufferStore(
        xe_texture_load_dest, block_offset_host,
        XeDXTOpaqueRowToRGB8(bgr_end_8in10, bgr_weights) +
            ((block.xxxx >> xesl_uint4(0u, 4u, 8u, 12u)) & 0xFu) * 0x11000000u);
    xesl_dont_flatten if (texel_index_host.y + 1u < load_info.height_texels) {
      xesl_writeTypedStorageBufferStore(
          xe_texture_load_dest, block_offset_host + elements_pitch_host,
          XeDXTOpaqueRowToRGB8(bgr_end_8in10, bgr_weights >> 8u) +
              ((block.xxxx >> xesl_uint4(16u, 20u, 24u, 28u)) & 0xFu) *
              0x11000000u);
      xesl_dont_flatten if (texel_index_host.y + 2u < load_info.height_texels) {
        xesl_writeTypedStorageBufferStore(
            xe_texture_load_dest, block_offset_host + 2u * elements_pitch_host,
            XeDXTOpaqueRowToRGB8(bgr_end_8in10, bgr_weights >> 16u) +
                ((block.yyyy >> xesl_uint4(0u, 4u, 8u, 12u)) & 0xFu) *
                0x11000000u);
        xesl_dont_flatten
        if (texel_index_host.y + 3u < load_info.height_texels) {
          xesl_writeTypedStorageBufferStore(
              xe_texture_load_dest,
              block_offset_host + 3u * elements_pitch_host,
              XeDXTOpaqueRowToRGB8(bgr_end_8in10, bgr_weights >> 24u) +
                  ((block.yyyy >> xesl_uint4(16u, 20u, 24u, 28u)) & 0xFu) *
                  0x11000000u);
        }
      }
    }
  }
xesl_entry_code_end_compute
